buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		mavenCentral()
	}

	dependencies {
		classpath('org.springframework.boot:spring-boot-gradle-plugin:2.2.5.RELEASE')
		classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0')
	}
}

plugins {
	id 'org.springframework.boot' version '2.2.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'java'
	id 'application'
}

apply plugin: 'com.palantir.docker'

group = ''
version = '0.0.1-SNAPSHOT'

mainClassName = "br.com.financial.operations.MainApplication"
sourceCompatibility = "11"
targetCompatibility = "11"


ext {
	set('springCloudVersion', "Greenwich.SR2")
	springfoxVersion = '2.9.2'
	mapStructVersion = '1.3.1.Final'
}

dependencies {

	//SPRING
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'

	// Swagger
	implementation "io.springfox:springfox-swagger2:${springfoxVersion}"
	implementation "io.springfox:springfox-swagger-ui:${springfoxVersion}"

	//GOOGLE LIBS
	implementation (group: 'com.google.code.gson', name: 'gson')

	//LOMBOK
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor("org.projectlombok:lombok")

	// MapStruct
	implementation "org.mapstruct:mapstruct:${mapStructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"
	testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"

	//DATABASE
	runtimeOnly 'com.h2database:h2'

	//TEST
	testCompileOnly 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testCompile(group: 'com.google.code.gson', name: 'gson')
	testImplementation "com.h2database:h2"
	testImplementation 'junit:junit'

}

compileJava {
	options.encoding = 'UTF-8'
}

compileTestJava {
	options.encoding = 'UTF-8'
}

test {
	systemProperty 'spring.profiles.active', "test"
}

repositories {
	mavenCentral()
}

bootJar {
	baseName = 'financial-operations'
	version =  '0.1.0'
}

task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/dependency")
}

docker {
	name "${project.group}/${bootJar.baseName}"
	copySpec.from(tasks.unpack.outputs).into("dependency")
	buildArgs(['DEPENDENCY': "dependency"])
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}
